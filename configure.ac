AC_INIT([interflop-verrou-vprec],[1.0],[interflop.project@gmail.com])
AM_SILENT_RULES([yes])
AC_CONFIG_AUX_DIR(autoconf)
AM_INIT_AUTOMAKE([subdir-objects -Wall -Werror foreign])
AC_CONFIG_MACRO_DIRS([m4])
AC_PROG_CC
AC_PROG_CXX
AM_PROG_AR
AC_CONFIG_HEADERS([config.h])
LT_INIT
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET


AC_ARG_ENABLE(wall, AS_HELP_STRING([--enable-wall],[Enable -Wall compilation flag]), [WALL_CFLAGS="yes"])
AM_CONDITIONAL([WALL_CFLAGS], [test "x$WALL_CFLAGS" = "xyes"])
if test "x$WALL_CFLAGS" = "xyes"; then
   AC_DEFINE([WALL_CFLAGS], [],  ["Enable -Wall compilation flag"])
   AC_MSG_NOTICE([-Wall is set])
fi

AC_ARG_ENABLE(interflop-stdlib, AS_HELP_STRING([--with-interflop-stdlib],[Link with interflop stdlib]), [INTERFLOP_STDLIB="yes"])
AM_CONDITIONAL([INTERFLOP_STDLIB], [test "x$INTERFLOP_STDLIB" = "xyes"])
if test "x$INTERFLOP_STDLIB" = "xyes"; then
   AC_DEFINE([INTERFLOP_STDLIB], [],  ["Link with interflop stdlib"])
   AC_MSG_NOTICE([link with interflop stdlib])
fi


#--enable-xoshiro
AC_CACHE_CHECK([verrou xoshiro prng], vg_cv_verrou_xoshiro,
  [AC_ARG_ENABLE(verrou-xoshiro,
    [  --enable-verrou-xoshiro          enables xoshiro prng for random and average rounding mode],
    [vg_cv_verrou_xoshiro=$enableval],
    [vg_cv_verrou_xoshiro=yes])])

AM_CONDITIONAL([USE_XOSHIRO], test x$vg_cv_verrou_xoshiro = xyes,[])
AC_SUBST(vg_cv_verrou_xoshiro)


AC_ARG_VAR(VERROU_NUM_AVG,[Number of AVG rounding per 64bit generated by mersenne twister or xoshiro])
AS_VAR_SET_IF([VERROU_NUM_AVG], [],[VERROU_NUM_AVG=1])

AS_CASE([$VERROU_NUM_AVG],
	[1],[],
	[2],[],
	[3],[],
	[4],[],
	[8],[],
	[*],[AC_MSG_ERROR(["invalid VERROU_NUM_AVG", $VERROU_NUM_AVG])]
)

AC_CACHE_CHECK([verrou-det-hash], vg_cv_verrou_det_hash,
[
AC_ARG_WITH(
	[verrou-det-hash],
	[  --with-verrou-det-hash=hash_name	hash algorithm for random_[com]det and average_[com]det: dietzfelbinger,multiply_shift,double_tabulation,mersenne_twister],
	[vg_cv_verrou_det_hash=$withval],
	[vg_cv_verrou_det_hash=double_tabulation]
)])

AS_CASE([$vg_cv_verrou_det_hash],
	[yes],[vg_cv_verrou_det_hash=double_tabulation],
	[dietzfelbinger],[echo "dietzfelbinger hash selected"],
	[multiply_shift],[echo "multiply_shift hash selected"],
	[double_tabulation],[echo "double_tabulation hash selected"],
	[mersenne_twister],[echo "mersenne_twister hash selected"],
	[*],[AC_MSG_ERROR(["invalid --with-verrou-det-hash : ", $vg_cv_verrou_det_hash])])

AC_SUBST(vg_cv_verrou_det_hash)

AC_CONFIG_FILES([
 Makefile
 common/Makefile
])
AC_OUTPUT